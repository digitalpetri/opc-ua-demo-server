plugins {
    id 'java'
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'com.isw.opcua'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    minimize {
        exclude(dependency("org.bouncycastle:.*:.*"))
        exclude(dependency("org.jetbrains.kotlin:kotlin-reflect:.*"))
    }
}

tasks.build.dependsOn tasks.shadowJar

distributions {
    main {
        baseName = "milo-demo-server"

        mkdir("logs")

        contents {
            into("lib") {
                from shadowJar
            }

            into("") {
                // hack to create create an empty "logs" directory in dist root
                def tmpDir = File.createTempDir()
                def logDir = tmpDir.toPath().resolve("logs").toFile()
                logDir.mkdir()

                from tmpDir
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.2.1"

    implementation "org.eclipse.milo:sdk-client:0.4.0-SNAPSHOT"
    implementation "org.eclipse.milo:sdk-server:0.4.0-SNAPSHOT"

    implementation "com.uchuhimo:konf:0.13.1"
    implementation 'ch.qos.logback:logback-classic:1.2.3'
}


