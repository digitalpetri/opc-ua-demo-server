plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.60"
    id "org.beryx.runtime" version "1.8.0"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

group "com.isw.opcua"
version "0.1-SNAPSHOT"

sourceCompatibility = 1.8

ext {
    milo_version = "0.4.0-SNAPSHOT"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    minimize {
        exclude dependency("org.bouncycastle:.*:.*")
        exclude dependency("org.jetbrains.kotlin:kotlin-reflect:.*")
    }
}

mainClassName = "com.isw.opcua.server/com.isw.opcua.server.MiloDemoServer"

application {
    mainClassName = "com.isw.opcua.server.MiloDemoServer"
    applicationName = "milo-demo-server"
}

jar {
    manifest {
        attributes(
            "Main-Class": "com.isw.opcua.server.MiloDemoServer",
            "Implementation-Title": "milo-demo-server",
            "X-Stack-Version": "$milo_version",
            "X-SDK-Version": "$milo_version"
        )
    }
}

runtime {
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
    modules = [
        "jdk.management",
        "java.management",
        "java.sql",
        "java.naming",
        "java.logging",
        "java.xml",
        "jdk.unsupported"
    ]
    imageDir = file("$buildDir/milo-demo-server")
    imageZip = file("$buildDir/milo-demo-server.zip")
}

task dist {
    dependsOn clean, runtimeZip
    description "Calls clean and then runtimeZip [default]"
}

defaultTasks "dist"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.2"

    implementation "org.eclipse.milo:sdk-client:$milo_version"
    implementation "org.eclipse.milo:sdk-server:$milo_version"

    implementation "com.uchuhimo:konf:0.13.1"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "io.github.soc:directories:11"
}


